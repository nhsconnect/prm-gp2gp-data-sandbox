index="gp2gp_nms_prod" sourcetype="gp2gpmi-rr" RequestorODS=$ods_code$
| eval has_conversation=if(isnotnull(ConversationID), 1, 0)
| where has_conversation == 1
| eval registration_type=coalesce(RegistrationType, "-")
| eval has_req_failure_time=if(isnotnull(RequestFailureTime), 1, 0)
| eval request_failure_point = coalesce(RequestFailurePoint, "-")
| eval request_failure_type = coalesce(RequestFailureType, "-")
| eval request_error_code = coalesce(RequestErrorCode, "-")
| eval has_extract_time=if(isnotnull(ExtractTime), 1, 0)
| eval has_extract_ack_time=if(isnotnull(ExtractAckTime), 1, 0)
| eval has_extract_failure_time=if(isnotnull(ExtractAckFailureTime), 1, 0)
| eval extract_ack_status = coalesce(ExtractAckStatus, "-")
| eval extract_ack_code = coalesce(ExtractAckCode, "-")
| eval extract_failure_type = coalesce(ExtractAckFailureType, "-")
| eval extract_ack_error_code = coalesce(ExtractAckErrorCode, "-")
| eval has_request_failure = if(isnotnull(RequestFailureTime) or isnotnull(RequestFailureType), 1, 0)
| eval has_extract_ack_failure = if(isnotnull(ExtractAckFailureTime) or isnotnull(ExtractAckFailureType), 1, 0)
| eval has_failure = if(has_request_failure == 1 or has_extract_ack_failure == 1, 1, 0)
| eval ack_sent =  if(ExtractAckStatus != "0", 1, 0)
| stats
list(registration_type) as registration_type
list(has_req_failure_time) as has_req_failure_ts
list(request_failure_point) as req_failure_point
list(request_failure_type) as req_failure_type
list(request_error_code) as req_error_code
list(has_extract_time) as has_ext_ts
list(has_extract_ack_time) as has_ext_ack_ts
list(has_extract_failure_time) as has_ext_fail_ts
list(extract_ack_status) as ext_ack_status
list(extract_ack_code) as ext_ack_code
list(extract_failure_type) as ext_fail_type
list(extract_ack_error_code) as ext_ack_error_code
max(ack_sent) as was_acked
max(has_failure) as has_fail
BY ConversationID
| eval ack_not_sent_and_no_failure = if(was_acked == 0 and has_fail == 0, 1, 0)
| where ack_not_sent_and_no_failure == 1
