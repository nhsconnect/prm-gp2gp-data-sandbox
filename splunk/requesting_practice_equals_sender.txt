index="gp2gp-mi" sourcetype="gppractice-RR" RequestorODS=$ods_code$
| where RequestorODS=SenderODS
| eval ConversationID=coalesce(ConversationID, "no ID")
| eval isNewPatient = if(RegistrationType == "1" and RequestFailurePoint == "60" and RequestFailureType == "5", 1, 0)
| eval isReturningPatient = if(RegistrationType == "2" and RequestFailurePoint == "60" and RequestFailureType == "5", 1, 0)
| eval hasPatientAlreadyAtPractice = if(RequestFailurePoint == "60" and RequestFailureType == "2", 1, 0)
| eval hasError = if(isnotnull(RequestFailureTime) or isnotnull(RequestFailureType), 1, 0)
| eval state = coalesce(case(
  isNewPatient == 1, "newRegistration",
  isReturningPatient == 1, "returningPatientNoPreviousGp",
  hasPatientAlreadyAtPractice = 1, "patientAlreadyAtPractice",
  hasError == 1, "unknownError"
  ), "unknown")
| table RegistrationTime, RegistrationSmartcardUID, ConversationID, state
| sort -RegistrationTime
